chunk ::= { stat [`;´] } [ laststat [`;´] ]

block ::= chunk

stat ::=  varlist `=´ explist | 
     functioncall | 
     do block end | 
     while exp do block end | 
     repeat block until exp | 
     if exp then block { elseif exp then block } [ else block ] end | 
     for Name `=´ exp `,´ exp [ `,´ exp ] do block end |
     for namelist in explist do block end |
     function funcname funcbody |
     local function Name funcbody |
     local namelist [ `=´ explist ]

laststat ::= return [ explist ] | break

varlist ::= var { `,´ var }

exp_args_back ::= exp_back | args_back

var ::= exp_front { exp_args_back } exp_back | Name

prefixexp ::= Name { exp_args_back } | `(´ exp `)´ { exp_args_back }

functioncall ::= exp_front { exp_args_back } args_back

exp_front ::= Name | `(´ exp `)´

exp_back ::= `[´ exp `]´ | `.´ Name

args_back ::= args | `:´ Name args

explist ::= { exp `,´ } exp

exp ::=  nil exp_p | false exp_p | true exp_p | Number exp_p | String exp_p | `...´ exp_p | functiondef exp_p | 
     prefixexp exp_p | tableconstructor exp_p  | unop exp exp_p 

exp_p ::= binop exp exp_p | epsilon

args ::= `(´ [ explist ] `)´ | tableconstructor | String 

functiondef ::= function funcbody

funcbody ::= `(´ [ parlist ] `)´ block end

tableconstructor ::= `{´ [ fieldlist ] `}´


fieldlist ::= field { fieldsep field } [ fieldsep ]

field ::= `[´ exp `]´ `=´ exp | Name `=´ exp | exp

funcname ::= Name { `.´ Name } [ `:´ Name ]

namelist ::= Name { `,´ Name }

parlist ::= namelist [ `,´ `...´ ] | `...´

fieldsep ::= `,´ | `;´

binop ::= `+´ | `-´ | `*´ | `/´ | `^´ | `%´ | `..´ | 
     `<´ | `<=´ | `>´ | `>=´ | `==´ | `~=´ | 
     and | or

unop ::= `-´ | not | `#´
