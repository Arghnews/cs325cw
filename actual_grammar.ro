chunk ::= { stat [`;´] } [ laststat [`;´] ]

block ::= chunk

stat ::=  varlist `=´ explist | 
     functioncall | 
     do block end | 
     while exp do block end | 
     repeat block until exp | 
     if exp then block { elseif exp then block } [ else block ] end | 
     for Name `=´ exp `,´ exp [ `,´ exp ] do block end |
     for namelist in explist do block end |
     function funcname funcbody |
     local function Name funcbody |
     local namelist [ `=´ explist ]

laststat ::= return [explist] | break

funcname ::= Name { `.´ Name } [ `:´ Name ]

varlist ::= var { `,´ var }

var ::= exp_front { exp_back | args_back } exp_back | Name

prefixexp ::= Name { exp_back | args_back } | `(´ exp `)´ { exp_back | args_back }

functioncall ::= exp_front { exp_back | args_back } args_back

exp_front ::= Name | `(´ exp `)´

exp_back ::= `[´ exp `]´ | `.´ Name

args_back ::= args | `:´ Name args

namelist ::= Name { `,´ Name }

explist ::= { exp `,´ } exp

exp ::=  nil exp_p | false exp_p | true exp_p | Number exp_p | String exp_p | `...´ exp_p | function exp_p | 
     prefixexp exp_p | tableconstructor exp_p  | unop exp exp_p 

exp_p ::= binop exp exp_p | epsilon

args ::=  `(´ [ explist ] `)´ | tableconstructor | String 

function ::= function funcbody

funcbody ::= `(´ [ parlist ] `)´ block end

parlist ::= namelist [ `,´ `...´ ] | `...´

tableconstructor ::= `{´ [ fieldlist ] `}´

fieldlist ::= field { fieldsep field } [ fieldsep ]

field ::= `[´ exp `]´ `=´ exp | Name `=´ exp | exp

fieldsep ::= `,´ | `;´

binop ::= `+´ | `-´ | `*´ | `/´ | `^´ | `%´ | `..´ | 
     `<´ | `<=´ | `>´ | `>=´ | `==´ | `~=´ | 
     and | or

unop ::= `-´ | not | `#´
